import Base#mbRT
import Geometry
import Extra

// :: COLLISION ::

T Hitbox
| circbox(rad: Number)
| polybox(pts: List(V3))

// :: GAME OBJECT ::

ObjectId : Type
  Number

ModelId : Type
  Number

T Anim
| anim(
  id  : Number,
  tik : Number)

T GameObject
| game_object(
  id  : ObjectId,
  hp  : Number,
  pos : V3,
  spd : Number,
  dir : V3,
  vel : V3,
  box : Hitbox,
  ani : Anim)

// Changes the object's fields
mut_object_fields(
  id: ObjectId -> ObjectId,
  hp: Number -> Number,
  pos: V3 -> V3,
  spd: Number -> Number,
  dir: V3 -> V3,
  vel: V3 -> V3,
  box: Hitbox -> Hitbox,
  ani: Anim -> Anim,
  obj: GameObject
) : GameObject
  case obj | game_object =>
  let id  = id(obj.id)
  let hp  = hp(obj.hp)
  let pos = pos(obj.pos)
  let spd = spd(obj.spd)
  let dir = dir(obj.dir)
  let vel = vel(obj.vel)
  let box = box(obj.box)
  let ani = ani(obj.ani)
  game_object(id,hp,pos,spd,dir,vel,box,ani)

// Object accessors / mutters

get_object_id(obj: GameObject)
  case obj | game_object =>
  obj.id

get_object_hp(obj: GameObject)
  case obj | game_object =>
  obj.hp

get_object_pos(obj: GameObject)
  case obj | game_object =>
  obj.pos

get_object_spd(obj: GameObject)
  case obj | game_object =>
  obj.spd

get_object_dir(obj: GameObject)
  case obj | game_object =>
  obj.dir

get_object_vel(obj: GameObject)
  case obj | game_object =>
  obj.vel

get_object_box(obj: GameObject)
  case obj | game_object =>
  obj.box

get_object_ani(obj: GameObject)
  case obj | game_object =>
  obj.ani

mut_object_id(fn: ObjectId -> ObjectId , obj: GameObject)
  mut_object_fields(
    fn, id(_), id(_), id(_),
    id(_), id(_), id(_), id(_),
    obj)

mut_object_hp(fn: Number -> Number, obj: GameObject)
  mut_object_fields(
    id(_), fn, id(_), id(_),
    id(_), id(_), id(_), id(_),
    obj)

mut_object_pos(fn: V3 -> V3, obj: GameObject)
  mut_object_fields(
    id(_), id(_), fn, id(_),
    id(_), id(_), id(_), id(_),
    obj)

mut_object_spd(fn: Number -> Number, obj: GameObject)
  mut_object_fields(
    id(_), id(_), id(_), fn,
    id(_), id(_), id(_), id(_),
    obj)

mut_object_dir(fn: V3 -> V3, obj: GameObject)
  mut_object_fields(
    id(_), id(_), id(_), id(_),
    fn, id(_), id(_), id(_),
    obj)

mut_object_vel(fn: V3 -> V3, obj: GameObject)
  mut_object_fields(
    id(_), id(_), id(_), id(_),
    id(_), fn, id(_), id(_),
    obj)

mut_object_box(fn: Hitbox -> Hitbox, obj: GameObject)
  mut_object_fields(
    id(_), id(_), id(_), id(_),
    id(_), id(_), fn, id(_),
    obj)

mut_object_ani(fn: Anim -> Anim, obj: GameObject)
  mut_object_fields(
    id(_), id(_), id(_), id(_),
    id(_), id(_), id(_), fn,
    obj)

// :: GAME MAP ::

GameMap : Type
  List(GameObject)

// Gets the position of an object on the game map
get_object_from_map(id: ObjectId, gm: GameMap)
: Maybe(GameObject)
  case gm
  | nil  => none(_)
  | cons =>
    case gm.head | game_object => 
    if gm.head.id .==. id then
      some(_ gm.head)
    else
      get_object_from_map(id, gm.tail)

// :: GAME ::

T Game
| game(map : GameMap)

get_game_map(game: Game) : GameMap
  case game | game =>
  game.map

// Gets an object by its id
get_object_by_id(id: ObjectId, gs: Game) : Maybe(GameObject)
  case gs | game =>
  get_object_from_map(id, gs.map)

// Gets the position of an object by its id
get_position_by_id(id: ObjectId, gs: Game) : V3
  case get_object_by_id(id, gs) as got
  | none => v3(0,0,0)
  | some => get_object_pos(got.value)

// :: TESTS ::

SRPX : Number; 0
STANCI : Number; 1
NEELIX : Number; 2
BELANNA : Number; 3

srpx : GameObject
  let id = 0
  let hp = 70
  let pos = v3(0, 0, 0)
  let spd = 0
  let dir = v3(0, -1, 0)
  let vel = v3(0, 0, 0)
  let box = circbox(10)
  let ani = anim(0, 0)
  game_object(id, hp, pos, spd, dir, vel, box, ani)

stanci : GameObject
  let id = 1
  let hp = 90
  let pos = v3(64, 0, 0)
  let spd = 0
  let dir = v3(1, -1, 0)
  let vel = v3(0, 0, 0)
  let box = circbox(8)
  let ani = anim(0, 0)
  game_object(id, hp, pos, spd, dir, vel, box, ani)

neelix : GameObject
  let id = 2
  let hp = 700
  let pos = v3(-64, 0, 0)
  let spd = 0
  let dir = v3(-1, -1, 0)
  let vel = v3(0, 0, 0)
  let box = v3(6,6,6)
  let box = circbox(8)
  let ani = anim(0, 0)
  game_object(id, hp, pos, spd, dir, vel, box, ani)

belanna : GameObject
  let id = 3
  let hp = 80
  let pos = v3(0, 64, 0)
  let spd = 0
  let dir = v3(0, -1, 0)
  let vel = v3(0, 0, 0)
  let box = circbox(14)
  let ani = anim(0, 0)
  game_object(id, hp, pos, spd, dir, vel, box, ani)

new_game : Game
  let map = [srpx, stanci, neelix, belanna]
  game(map)

// GAME INPUT

T GameAction
| game_sdir(id: ObjectId, dir: V3) // stick direction
| game_key0(id: ObjectId, pos: V3) // left down
| game_key1(id: ObjectId, pos: V3) // middle down
| game_key2(id: ObjectId, pos: V3) // right down
| game_key3(id: ObjectId, pos: V3) // space down
| game_key4(id: ObjectId, pos: V3) // extra down
| game_key5(id: ObjectId, pos: V3) // shift down
| game_text(id: ObjectId, txt: String) // chat message

// Modifies every object in the game
map_game_objects(
  fn : GameObject -> GameObject,
  gm : Game
) : Game
  game(map_list(__ fn, get_game_map(gm)))

// Modifies the object with given id
// TODO: Optimize
modify_object_by_id(
  id: ObjectId,
  fn: GameObject -> GameObject,
  gm: Game
) : Game
  let fn2 = (obj) =>
    case obj | game_object =>
    if obj.id .==. id then
      fn(obj)
    else
      obj
  game(map_list(GameObject; _ fn2, get_game_map(gm)))

// Apply an action to the game state
exec_game_action(action: GameAction, gm: Game) : Game
  case action
  | game_sdir =>
    let fn = (obj) =>
      case obj | game_object =>
      // Change its direction if analog isn't centered
      let dir_fn =
        if len_v3(action.dir) .==. 0 then
          (old_dir) => old_dir
        else
          (old_dir) => action.dir
      // Set its speed if analog isn't centered
      let spd_fn =
        if len_v3(action.dir) .==. 0 then
          (old_spd) => 0
        else
          (old_spd) =>
            if obj.id .==. 3 then
              3
            else
              6
      // Modify the object using the logic above
      let obj = mut_object_dir(dir_fn, obj)
      let obj = mut_object_spd(spd_fn, obj)
      obj
    modify_object_by_id(action.id, fn, gm)
  | game_key0 =>
    let fn = mut_object_pos((a) => action.pos)
    modify_object_by_id(action.id, fn, gm)
  | game_key1 => gm
  | game_key2 => gm
  | game_key3 =>
    let fn = (obj) =>
      mut_object_ani((a) => anim(1, 0), obj)
    modify_object_by_id(action.id, fn, gm)
  | game_key4 => gm
  | game_key5 => gm
  | game_text => gm

// Tick-wise interaction of `a` with `b`
interact_with(this:GameObject, that:GameObject): GameObject
  case this | game_object =>
  case that | game_object =>
  case this.box
  | circbox => 
    case that.box
    | circbox =>  
      let dst = dist_v3(this.pos, that.pos)
      let rad = this.box.rad .+. that.box.rad
      if (dst .>. that.box.rad) .&. (dst .<. rad) then
        let out_dir = norm_v3(sub_v3(this.pos, that.pos))
        let out_vec = scale_v3(rad .-. dst, out_dir)
        let new_pos = add_v3(this.pos, out_vec)
        mut_object_pos((old) => new_pos, this)
      else
        this
    | polybox =>
      this
  | polybox =>
    this

// Game's main loop
exec_game_turn(gm: Game) : Game
  case gm | game =>
  let tick = (obj) =>
    case obj | game_object =>
    let new_id  = obj.id
    let new_hp  = obj.hp
    let new_spd = obj.spd
    let new_dir = obj.dir
    let new_vel = obj.vel
    let new_box = obj.box
    let new_ani =
      case obj.ani
      | anim =>
        let id  = obj.ani.id
        let tik = obj.ani.tik
        let res =
          let a = (id .==. 1) .&. (tik .==. 15)
          let b = (id .==. 3) .&. (tik .==. 18)
          a .|. b
        let new_id = if res then 0 else id
        let new_tik = if res then 0 else tik .+. 1
        anim(new_id, new_tik)
    let new_pos = add_v3(obj.pos, scale_v3(obj.spd, obj.dir))
    let trg_pos = v3(0,64,0)
    let new_obj = game_object(
      new_id, new_hp, new_pos, new_spd,
      new_dir, new_vel, new_box, new_ani)
    let int_fun = (that, this) => interact_with(this, that)
    fold_list(GameObject; _ new_obj, int_fun, gm.map)
  game(map_list(GameObject; _ tick, gm.map))

// TODO: very hard coded, comment, improve, etc.
get_object_model_id(obj: GameObject) : Number 
  case obj | game_object =>
  case obj.ani | anim =>
  if obj.id .==. 0 then
    // idle/walk
    if obj.ani.id .==. 0 then
      if (obj.spd .<. 0.1) then
        MIN_IDLE_0 .+. ((obj.ani.tik ./. 3) .>>>. 0) .%. 3
      else
        MIN_RUN_0 .+. ((obj.ani.tik ./. 3) .>>>. 0) .%. 5
    // attack
    else
      MIN_ATTACKA_0 .+. ((obj.ani.tik ./. 3) .>>>. 0) .%. 6
  else if obj.id .==. 3 then
    // idle/walk
    if obj.ani.id .==. 0 then
      if (obj.spd .<. 0.1) then
        ZOIN_IDLE_0 .+. ((obj.ani.tik ./. 3) .>>>. 0) .%. 5
      else
        ZOIN_WALK_0 .+. ((obj.ani.tik ./. 3) .>>>. 0) .%. 6
    // attack
    else
      ZOIN_ATTACKA_0 .+. ((obj.ani.tik ./. 3) .>>>. 0) .%. 7
  else
    SIMPLAO_IDLE1 .+. ((obj.ani.tik ./. 3) .>>>. 0) .%. 4

SIMPLAO_IDLE1 0
SIMPLAO_IDLE2 1
SIMPLAO_IDLE3 2
SIMPLAO_IDLE4 3
SIMPLAO_ATK1 4
SIMPLAO_ATK2 5
SIMPLAO_ATK3 6
SIMPLAO_ATK4 7
SIMPLAO_MOVE1 8
SIMPLAO_MOVE2 9
SIMPLAO_MOVE3 10
SIMPLAO_MOVE4 11
SIMPLAO_PODER1 12
SIMPLAO_PODER2 13
SIMPLAO_PODER3 14
SIMPLAO_PODER4 15
SIMPLAO_PODER5 16
SIMPLAO_PODER6 17
SIMPLAO_PODER7 18
MIN_IDLE_0 19
MIN_IDLE_1 20
MIN_IDLE_2 21
MIN_RUN_0 22
MIN_RUN_1 23
MIN_RUN_2 24
MIN_RUN_3 25
MIN_RUN_4 26
MIN_ATTACKA_0 27
MIN_ATTACKA_1 28
MIN_ATTACKA_2 29
MIN_ATTACKA_3 30
MIN_ATTACKA_4 31
MIN_ATTACKA_5 32
ZOIN_IDLE_0 33
ZOIN_IDLE_1 34
ZOIN_IDLE_2 35
ZOIN_IDLE_3 36
ZOIN_IDLE_4 37
ZOIN_ATTACKA_0 38
ZOIN_ATTACKA_1 39
ZOIN_ATTACKA_2 40
ZOIN_ATTACKA_3 41
ZOIN_ATTACKA_4 42
ZOIN_ATTACKA_5 43
ZOIN_ATTACKA_6 44
ZOIN_WALK_0 45
ZOIN_WALK_1 46
ZOIN_WALK_2 47
ZOIN_WALK_3 48
ZOIN_WALK_4 49
ZOIN_WALK_5 50
ZOIN_WALK_6 51
